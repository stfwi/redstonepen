// @file build.gradle
// Redstone Pen mod gradle build relay (mc1.19.x)
plugins {
  id 'eclipse'
  id 'maven-publish'
  id 'net.minecraftforge.gradle' version '5.1.+'
}
java.toolchain.languageVersion = JavaLanguageVersion.of(17)
//-----------------------------------------------------------------------------
version = "${version_mod}"
group = "wile.${meta_modid}"
archivesBaseName = "${meta_modid}-${version_minecraft}-forge"

repositories {
  maven { name = "Progwml6 maven"; url = "https://dvs1.progwml6.com/files/maven/" } // JEI files
  maven { name = "ModMaven"; url = "modmaven.k-4u.nl" } // JEI files, fallback
  flatDir { dir 'libs' } // Mod jar dependencies in ./libs
}

minecraft {
  mappings channel: "official", version: "${version_minecraft}"
  // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.markers', '' // SCAN,REGISTRIES,REGISTRYDUMP
      property 'forge.logging.console.level', 'info'
      mods { redstonepen { source sourceSets.main } } }
    server {
      workingDirectory project.file('run')
      property 'forge.logging.markers', '' // SCAN,REGISTRIES,REGISTRYDUMP
      property 'forge.logging.console.level', 'info'
      mods { redstonepen { source sourceSets.main } }
    }
  }
}

dependencies {
  minecraft "net.minecraftforge:forge:${version_minecraft}-${version_forge}"
}

processResources {
  var property_replacements = [
    meta_modid:meta_modid, meta_modname:meta_modname, meta_author:meta_author, meta_license:meta_license,
    meta_description:meta_description, meta_issues:meta_issues, meta_updateurl:meta_updateurl,
    meta_download:meta_download,
    version_mod: version_mod,
    version_minecraft:version_minecraft,
    version_forge:version_forge,
    version_range_loader:version_range_loader,
    version_range_forge:version_range_forge,
    version_pack_format: version_pack_format,
  ]
  inputs.properties property_replacements
  filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
    expand property_replacements + [project: project]
  }
  outputs.upToDateWhen { false }
  doLast { file("${sourceSets.main.output.resourcesDir}/.gitversion-redstonepen").text = 'git log "-1" "--format=%h"'.execute().in.text.trim() }
}

jar {
  manifest {
    attributes([
      "Specification-Title": "${meta_modid}",
      "Specification-Vendor": "wilechaote",
      "Specification-Version": "1", // We are version 1 of ourselves
      "Implementation-Title": project.name,
      "Implementation-Version": "${version_mod}",
      "Implementation-Vendor" :"wilechaote",
      "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}

jar.finalizedBy('reobfJar')

// Signing
import net.minecraftforge.gradle.common.tasks.SignJar
project.afterEvaluate {
  task signJar(type: SignJar, dependsOn: jar) {
    onlyIf { project.hasProperty("keystore_file") }
    if(project.hasProperty("keystore_file")) {
      keyStore = project.getProperty("keystore_file")
      alias = project.getProperty("keystore_alias")
      storePass = project.getProperty("keystore_pass")
      keyPass = project.getProperty("keystore_keypass")
      inputFile = jar.archiveFile
      outputFile = jar.archiveFile
    } else {
      logger.warn("[WARNING] Signing skipped.")
    }
  }
  reobfJar.finalizedBy('signJar')
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}
